//把只能更新单条数据的代码优化称可以批量更新的代码

 public static void updateAvgCaseResolutionDays(Id accountId){
        Integer totalDays = 0, totalCases = 0;

        // retrive all the cases for this account
        // get resolution days for each account (ClosedDate - CreatedDate)
        for(Case caseObj : [SELECT Id, CreatedDate, ClosedDate
                             FROM Case
                             WHERE IsClosed=true
                            	AND AccountId =: accountId]){

            // increase number of cases
            totalCases++;
            // get duration in days and add it to totalDays
            totalDays += caseObj.CreatedDate.date().daysBetween(caseObj.ClosedDate.date());
        }

        Decimal resolutionDays = totalDays/totalCases;

        // update account object
        Account acc = new Account(Id=accountId, Avg_Case_Resolution_Days__c = resolutionDays);
        update acc;
    }

////////////////////////////////////////////
public class AccountController {
    public static void updateAvgCaseResolutionDays(List<Id> accountIds){
        Map<Id,Integer> accountTotalCasesMap = new Map<Id,Integer>();
        Map<Id,Integer> accountTotalCasesDaysMap = new Map<Id,Integer>();

        //Integer totalDays = 0, totalCases = 0;
        // retrive all the cases for this account
        // get resolution days for each account (ClosedDate - CreatedDate)
        for(Case caseObj : [SELECT Id, CreatedDate, ClosedDate, AccountId
                            FROM Case
                            WHERE IsClosed=true
                            AND AccountId IN :accountIds]){

            //if map does not have current account id,then add it
            if (! accountTotalCasesMap.containsKey(caseObj.AccountId)) {
                accountTotalCasesMap.put(caseObj.AccountId, 0);
                accountTotalCasesDaysMap.put(caseObj.AccountId, 0);
            }
            //获取map中id的案例总数
            Integer totalCases = accountTotalCasesMap.get(caseObj.AccountId);
            // increase number of cases
            totalCases++;
            accountTotalCasesMap.put(caseObj.AccountId, totalCases);

            //获取map中id的总天数
            Integer totalDays = accountTotalCasesDaysMap.get(caseObj.AccountId);
            // get duration in days and add it to totalDays
            totalDays += caseObj.CreatedDate.date().daysBetween(caseObj.ClosedDate.date());
            accountTotalCasesDaysMap.put(caseObj.AccountId, totalDays);
        }

        //list of account to update
        List<Account> accountToUpdate = new List<Account>();

        for (Id accountId : accountTotalCasesMap.keySet()) {
            Decimal resolutionDays = accountTotalCasesDaysMap.get(accountId)/accountTotalCasesMap.get(accountId);
            // update account object
            accountToUpdate.add(new Account(Id = accountId, Avg_Case_Resolution_Days__c = resolutionDays)); 
        }

        update accountToUpdate;
    }
}


